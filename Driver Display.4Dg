#platform "uLCD-28PT"

/****************************************************************************************
*                                                                                       *
*  Driver Display                                                                       *
*                                                                                       *
****************************************************************************************/

#inherit "4DGL_16bitColours.fnc"

#inherit "VisualConst.inc"

#inherit "Driver DisplayConst.inc"

#constant BUFFER_SIZE 50
#constant BUF_BUF, BUF_SIZE, BUF_START, BUF_DATA_SZ
#constant BUF_STRUCT_SIZE 4
#constant ST_NONE, ST_VELOCITY, ST_CURRENT, ST_AVG_VOLTAGE, ST_AVG_TEMP, ST_EFFICIENCY, ST_MESSAGE
#constant LINE_WIDTH 320


/*
 * printBuffer
 *
 * Print the data currently stored in the buffer (not the whole buffer)
 *
 * Parameters:
 * - buffer_s: a buffer array-struct
 *
 * Returns:
 * number of characters printed
 */
func printBuffer(var buffer_s)
    var b_end;
    b_end := (buffer_s[BUF_START] + buffer_s[BUF_DATA_SZ]) % buffer_s[BUF_SIZE];
    var buf_ptr;
    buf_ptr := buffer_s[BUF_BUF];
    var buf_start;
    buf_start := buffer_s[BUF_START];
    var char_count := 0;

    if (buffer_s[BUF_DATA_SZ] == buffer_s[BUF_SIZE]) // overflow condition
        putch(buf_ptr[buf_start]);
        buf_start := (buf_start + 1) % buffer_s[BUF_SIZE];
        char_count += 1;
    endif

    while (buf_start != b_end)
        putch(buf_ptr[buf_start]);
        buf_start := (buf_start + 1) % buffer_s[BUF_SIZE];
        char_count += 1;
    wend

    return char_count;
endfunc


/*
 * showBuffer
 *
 * Show the current state of the buffer
 *
 * Parameters:
 * - buffer: a buffer array-struct
 */
func showBuffer(var buffer_s)
    var i;
    var buf_ptr;
    buf_ptr := buffer_s[BUF_BUF];
    for(i:=0; i < buffer_s[BUF_SIZE]; i++)
        putch(buf_ptr[i]);
    next
endfunc


/*
 * storeInBuffer
 *
 * Add a character to a given buffer
 *
 * Parameters:
 * - buffer_s: a buffer array-struct
 * - char: character to add to the buffer
 */
func storeInBuffer(var buffer_s, var char)
    var buf_ptr;
    buf_ptr := buffer_s[BUF_BUF];
    buf_ptr[(buffer_s[BUF_START] + buffer_s[BUF_DATA_SZ]) % buffer_s[BUF_SIZE]] := char;

    if (buffer_s[BUF_DATA_SZ] == buffer_s[BUF_SIZE])
        buffer_s[BUF_START] := (buffer_s[BUF_START] + 1) % buffer_s[BUF_SIZE];
    else if (buffer_s[BUF_DATA_SZ] < buffer_s[BUF_SIZE])
        buffer_s[BUF_DATA_SZ]++;
    endif
endfunc

/*
 * resetBuffer
 *
 * Reset the buffer
 *
 * Parameters:
 * - buffer_s: a buffer array-struct
 */
func resetBuffer(var buffer_s)
    buffer_s[BUF_START] := (buffer_s[BUF_START] + buffer_s[BUF_DATA_SZ]) % BUFFER_SIZE;
    buffer_s[BUF_DATA_SZ] := 0;
endfunc

/*
 * bufferEqual
 *
 * Compare current buffer data to a string
 *
 * Paramters:
 * - buffer_s: a buffer array-struct
 * - str: string to compare to
 *
 * return:
 * 0 if not equal, 1 if equal
 */
func bufferEqual(var buffer_s, var str)
    var b_end;
    var str_index := 0;
    b_end := (buffer_s[BUF_START] + buffer_s[BUF_DATA_SZ]) % buffer_s[BUF_SIZE];
    var buf_ptr;
    buf_ptr := buffer_s[BUF_BUF];
    var buf_start;
    buf_start := buffer_s[BUF_START];
    var char;

    if (buffer_s[BUF_DATA_SZ] == buffer_s[BUF_SIZE]) // overflow condition
        if (buf_ptr[buf_start] != (str[str_index] & 0xFF))
            return 0;
        endif
        buf_start := (buf_start + 1) % buffer_s[BUF_SIZE];
        str_index += 1;
    endif

    while (str_index != strlen(str))
        if (buf_ptr[buf_start] != (str[str_index] & 0xFF))
            return 0;
        endif
        buf_start := (buf_start + 1) % buffer_s[BUF_SIZE];
        str_index += 1;
    wend

    if (buf_start != b_end)
        return 0;
    endif
    return 1;
endfunc


/*
 * clearLine
 *
 * Clear a line, starting at a certain offset
 *
 * Parameters:
 * - offset: the pixel offset at which to begin
 */
func clearLine(var offset)
    var pixelsToClear;
    pixelsToClear := LINE_WIDTH - offset;
    var spaceChars;
    spaceChars := pixelsToClear / charwidth(' ');
    var i;
    for(i := 0; i < spaceChars; i++)
        print(" ");
    next
endfunc


func main()
    // Display initialization code
    gfx_Set(SCREEN_MODE,LANDSCAPE_R);

    putstr("Waiting...\n");

    if (!(disk:=file_Mount()))
        while(!(disk :=file_Mount()))
            putstr("Drive not mounted...");
            pause(200);
            gfx_Cls();
            pause(200);
        wend
    endif
    gfx_TransparentColour(0x0020);
    gfx_Transparency(ON);

    hndl := file_LoadImageControl("DRIVER~1.dat", "DRIVER~1.gci", 1);

    // Incoming data initialization
    var code_array[] := ["velo", "curr", "avlt", "atmp", "effy", "text"];
    var disp_text_array[] := ["Velocity", "Current", "Average Voltage", "Average Temperature", "Efficiency", "Message"];
    var state_array[] := [ST_VELOCITY, ST_CURRENT, ST_AVG_VOLTAGE, ST_AVG_TEMP, ST_EFFICIENCY, ST_MESSAGE];
    var num_fields := 6;
    var field_state := ST_NONE;
    var debug_str := "empty";
    var msg_lines := 0;

    // Buffer initialization
    var buffer[BUFFER_SIZE];
    var buf_struct[BUF_STRUCT_SIZE];
    buf_struct[BUF_BUF] := buffer;
    buf_struct[BUF_SIZE] := BUFFER_SIZE;
    buf_struct[BUF_START] := 0;
    buf_struct[BUF_DATA_SZ] := 0;

    // Miscellaneous initialization
    var char;
    var chars;
    var i;
    gfx_Cls();

    repeat
        char := serin(); // char < 0 when no input received.
        if (char >= 0 && char != '|' && char != ':')
            storeInBuffer(buf_struct, char);
        else if (char == '|')

            for (i := 0; i < num_fields; i++)
                if (field_state == state_array[i])
                    txt_MoveCursor(i, 0);
                    clearLine(0);
                    if (field_state == ST_MESSAGE)
                        var msg_line;
                        for (msg_line := 1; msg_line < msg_lines; msg_line++)
                            txt_MoveCursor(i + msg_line, 0);
                            clearLine(0);
                        next
                    endif
                    txt_MoveCursor(i, 0);
                    putstr(disp_text_array[i]);
                    print(": ");
                endif
            next

            if (field_state != ST_NONE)
                chars := printBuffer(buf_struct);
                if (field_state == ST_MESSAGE)
                    var chars_per_line;
                    chars_per_line := LINE_WIDTH / charwidth(' ');
                    msg_lines := (chars - (chars % chars_per_line))
                        / chars_per_line + 1;
                endif
                field_state := ST_NONE;
            endif

            resetBuffer(buf_struct);
        else if (char == ':')
            for (i := 0; i < num_fields; i++)
                if (bufferEqual(buf_struct, code_array[i]))
                    field_state := state_array[i];
                endif
            next

            resetBuffer(buf_struct);
        endif

    forever
endfunc
